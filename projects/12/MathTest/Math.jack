// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Math.jack

/**
 * A library of commonly used mathematical functions.
 * Note: Jack compilers implement multiplication and division using OS method calls.
 */
class Math {
    static Array twoToThe;

    /** Initializes the library. */
    function void init() {
        let twoToThe = Array.new(16);
        let twoToThe[0] = 1;
        let twoToThe[1] = 2;
        let twoToThe[2] = 4;
        let twoToThe[3] = 8;
        let twoToThe[4] = 16;
        let twoToThe[5] = 32;
        let twoToThe[6] = 64;
        let twoToThe[7] = 128;
        let twoToThe[8] = 256;
        let twoToThe[9] = 512;
        let twoToThe[10] = 1024;
        let twoToThe[11] = 2048;
        let twoToThe[12] = 4096;
        let twoToThe[13] = 8192;
        let twoToThe[14] = 16384;
        let twoToThe[15] = 32767 + 1;

        return;
    }

    /** Returns the absolute value of x. */
    function int abs(int x) {
        if (x < 0) {
            return -x;
        }

        return x;
    }

    /** Returns the product of x and y. 
     *  When a Jack compiler detects the multiplication operator '*' in the 
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x*y and multiply(x,y) return the same value.
     */
    function int multiply(int x, int y) {
        var int sum, shiftedX, i;
        let sum = 0;
        let shiftedX = x;
        let i = 0;
        while (i < 16) {
            if (Math.bit(y, i) = 1) {
                let sum = sum + shiftedX;
            }
            let shiftedX = shiftedX + shiftedX;
            let i = i + 1;
        }

        return sum;
    }

    function int bit(int x, int i) {
        if ((x & twoToThe[i]) = 0) {
            return 0;
        }

        return 1;
    }

    function int getTwoToThe(int x) {
        return twoToThe[x];
    }

    function int mod(int x, int y) {
        var int div;
        let div = x / y;

        return x - (div * y);
    }

    /** Returns the integer part of x/y.
     *  When a Jack compiler detects the multiplication operator '/' in the 
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x/y and divide(x,y) return the same value.
     */
    function int divide(int x, int y) {
        var int absX, absY, absY2, q, product, result;
        let absX = Math.abs(x);
        let absY = Math.abs(y);
        if (absY > absX) {
            return 0;
        }
        let absY2 = absY + absY;
        if (absY2 < 0) { // overflow
            return 1;
        }
        let q = Math.divide(absX, absY2);
        let product = q * absY2;
        if (product < 0) { // overflow
            return 1;
        }
        if ((absX - product) < absY) {
            let result = q + q;
        } else {
            let result = (q + q) + 1;
        }

        if (((x < 0) & (y > 0)) | ((x > 0) & (y < 0))) { 
            return -result;
        }

        return result;
    }

    /** Returns the integer part of the square root of x. */
    function int sqrt(int x) {
        var int y, i, sum, sumSquare, maxI;
        let y = 0;
        let i = 7; // (16 / 2) - 1
        while (i > -1) {
            let sum = y + twoToThe[i];
            let sumSquare = sum * sum;
            if (((sumSquare < x) | (sumSquare = x)) & (sumSquare > 0)) {
                let y = sum;
            }
            let i = i - 1;
        }

        return y;
    }

    /** Returns the greater number. */
    function int max(int a, int b) {
        if (a > b) {
            return a;
        }

        return b;
    }

    /** Returns the smaller number. */
    function int min(int a, int b) {
        if (a < b) {
            return a;
        }

        return b;
    }
}
