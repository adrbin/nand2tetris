// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Memory.jack

/**
 * This library provides two services: direct access to the computer's main
 * memory (RAM), and allocation and recycling of memory blocks. The Hack RAM
 * consists of 32,768 words, each holding a 16-bit binary number.
 */ 
class Memory {

    static Array ram;
    static Array freeList;

    /** Initializes the class. */
    function void init() {
        let ram = 0;
        let freeList = 2048; // heapBase
        let freeList[0] = 0;
        let freeList[1] = 14334;

        return;
    }

    /** Returns the RAM value at the given address. */
    function int peek(int address) {
        return ram[address];
    }

    /** Sets the RAM value at the given address to the given value. */
    function void poke(int address, int value) {
        let ram[address] = value;
        return;
    }

    /** Finds an available RAM block of the given size and returns
     *  a reference to its base address. */
    function int alloc(int size) {
        var Array segment, newSegment;
        var int newSegmentSize;

        let newSegmentSize = size + 2;
        let segment = freeList;
        while (~(segment = 0)) {
            if (segment[1] > (size + 1)) {
                let newSegment = (segment + segment[1]) - newSegmentSize;
                let newSegment[1] = size;
                let segment[1] = segment[1] - newSegmentSize;
                return newSegment + 2;
            }

            let segment = segment[0];
        }

        do Sys.error(1);
        return 0;
    }

    /** De-allocates the given object (cast as an array) by making
     *  it available for future allocations. */
    function void deAlloc(Array o) {
        var Array segment, oSegment;
        var int oSegmentSize;

        let segment = freeList;
        let oSegment = o - 2;
        let oSegmentSize = oSegment[1] + 2;
        while (~(segment = 0)) { // deFrag
            if ((segment + segment[1]) = oSegment) {
                let segment[1] = segment[1] + oSegmentSize;
                return;
            }

            let segment = segment[0];
        }

        let oSegment[0] = freeList;
        let freeList = oSegment;

        return;
    }
}
